MODULE BITSS_POTENTIAL
  USE COMMONS, ONLY : NOPT

  IMPLICIT NONE
  DOUBLE PRECISION :: TARGET_DIST, KE, KD

  CONTAINS


  DOUBLE PRECISION FUNCTION DISTANCE(COORDS1, COORDS2, GRAD)
    DOUBLE PRECISION, INTENT(IN) :: COORDS1(NOPT), COORDS2(NOPT)
    ! DOUBLE PRECISION, :: DISTANCE
    DOUBLE PRECISION, OPTIONAL :: GRAD(NOPT)
    DOUBLE PRECISION :: DIFF(NOPT)
    DIFF = COORDS1 - COORDS2
    DISTANCE = NORM2(DIFF)
    IF (PRESENT(GRAD)) GRAD = DIFF / DISTANCE
  END FUNCTION DISTANCE


  SUBROUTINE BITSS_E(COORDS, ENERGY)
    DOUBLE PRECISION, INTENT(IN) :: COORDS(2*NOPT)
    DOUBLE PRECISION, INTENT(OUT) :: ENERGY
    DOUBLE PRECISION :: E1, E2, G1(NOPT), G2(NOPT), D, GD, RMS
    ASSOCIATE(COORDS1=>COORDS(1:NOPT), COORDS2=>COORDS(NOPT+1:2*NOPT))

    CALL POTENTIAL(COORDS1, E1, G1, .FALSE., .FALSE., RMS, .FALSE., .FALSE.)
    CALL POTENTIAL(COORDS2, E2, G2, .FALSE., .FALSE., RMS, .FALSE., .FALSE.)
    D = DISTANCE(COORDS1, COORDS2)
    ENERGY = E1 + E2 + KE*(E1-E2)**2 + KD*(D - TARGET_DIST)**2
    END ASSOCIATE
  END SUBROUTINE BITSS_E


  SUBROUTINE BITSS_EG(COORDS, ENERGY, GRAD)
    DOUBLE PRECISION, INTENT(IN) :: COORDS(2*NOPT)
    DOUBLE PRECISION, INTENT(OUT) :: ENERGY, GRAD(2*NOPT)
    DOUBLE PRECISION :: E1, E2, G1(NOPT), G2(NOPT), D, GD(NOPT), RMS
    ASSOCIATE(COORDS1=>COORDS(1:NOPT), COORDS2=>COORDS(NOPT+1:2*NOPT))
    ASSOCIATE(GRAD1=>GRAD(1:NOPT), GRAD2=>GRAD(NOPT+1:2*NOPT))

    CALL POTENTIAL(COORDS1, E1, G1, .TRUE., .FALSE., RMS, .FALSE., .FALSE.)
    CALL POTENTIAL(COORDS2, E2, G2, .TRUE., .FALSE., RMS, .FALSE., .FALSE.)
    D = DISTANCE(COORDS1, COORDS2, GD)
    ENERGY = E1 + E2 + KE*(E1-E2)**2 + KD*(D - TARGET_DIST)**2
    GRAD1 = (1 + 2*KE*(E1-E2))*G1 + 2*KD*(D - TARGET_DIST)*GD
    GRAD2 = (1 + 2*KE*(E2-E1))*G2 - 2*KD*(D - TARGET_DIST)*GD
    END ASSOCIATE
    END ASSOCIATE
  END SUBROUTINE BITSS_EG


END MODULE BITSS_POTENTIAL
